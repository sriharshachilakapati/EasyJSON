apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "com.goharsha"
archivesBaseName = "easyjson"
version = "0.1"

// Check if the required properties are included, so we prevent our
// build from failing.
if (project.hasProperty('ossrhUsername'))
{
    project.ext.ossrhUsername = ossrhUsername
    project.ext.ossrhPassword = ossrhPassword
}
else
{
    // We might be running on another system where he is not
    // the owner who can push to OSSRH through maven. Set
    // default values so he can still build the library locally.
    project.ext.ossrhUsername = ''
    project.ext.ossrhPassword = ''
}

repositories {
    jcenter()
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task gwtJar(type: Jar) {
    classifier = "gwt"
    from sourceSets.main.allSource
    from sourceSets.main.output
}

artifacts {
    archives gwtJar, javadocJar, sourcesJar
}

dependencies {
    compile 'com.google.gwt:gwt-user:2.8.0-beta1'
    testCompile 'junit:junit:4.12'
}

signing {
    // Only sign the archives if there is a username. This allows one to build the library
    // on systems with no username set.
    if (ossrhUsername != '')
        sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: project.ext.ossrhUsername, password: project.ext.ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: project.ext.ossrhUsername, password: project.ext.ossrhPassword)
            }

            pom.project {
                name 'EasyJSON'
                packaging 'jar'
                description 'An easy to use GWT compatible JSON parser for Java'
                url 'https://github.com/sriharshachilakapati/EasyJSON/'

                scm {
                    connection 'scm:git:git@github.com:sriharshachilakapati/EasyJSON.git'
                    developerConnection 'scm:git:git@github.com:sriharshachilakapati/EasyJSON.git'
                    url 'git@github.com:sriharshachilakapati/EasyJSON.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'https://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'sriharshachilakapati'
                        name 'Sri Harsha Chilakapati'
                        email 'sriharshachilakapati@gmail.com'
                    }
                }
            }
        }
    }
}